WHITESPACE  = _{ " " | "	" | NEWLINE | COMMENT }
NEWLINE     = _{ "\r\n" | "\n" }
COMMENT     = _{ "/*" ~ (!("*/") ~ ANY)* ~ "*/" | "//" ~ (!(NEWLINE) ~ ANY)* }

identifier  = @{ (ASCII_ALPHANUMERIC | "_")+ }
number      = @{ ASCII_DIGIT+ }
string      = @{ "\"" ~ (!("\"") ~ ANY)* ~ "\"" }

file        =  { SOI ~ (directive | item)* ~ EOI }

directive   =  { "#" ~ directive_name ~ directive_value? }
directive_name = @{ identifier }
directive_value = @{ (!NEWLINE ~ ANY)+ }
item        =  { script_def | class_def | object_def }

script_def  =  { "script" ~ (number | identifier) ~ block }
class_def   =  { "class"  ~ identifier ~ block }
object_def  =  { "object" ~ identifier ~ string ~ block }

block       =  { "{" ~ stmt* ~ "}" }
stmt        = _{ block | expr_stmt | control_stmt | class_stmt | verb_stmt | var_decl | prop_assign | state_stmt }

expr_stmt   =  { expr ~ ";" }
control_stmt=  { if_stmt | while_stmt }
class_stmt  =  { "class" ~ identifier ~ ";" }
verb_stmt   =  { "verb" ~ identifier ~ (";" | block) }
var_decl    =  { identifier ~ identifier ~ "=" ~ expr ~ ";" }
prop_assign =  { identifier ~ (number | string | identifier) ~ ";" }
state_stmt  =  { "state" ~ number ~ (identifier ~ "=" ~ primary)* ~ ";" }

if_stmt     =  { "if" ~ "(" ~ expr ~ ")" ~ block ~ ("else" ~ block)? }
while_stmt  =  { "while" ~ "(" ~ expr ~ ")" ~ block }

expr        =  _{ assignment }
assignment  =  { logical_or ~ ("=" ~ assignment)? }
logical_or  =  { logical_and ~ ("||" ~ logical_and)* }
logical_and =  { equality ~ ("&&" ~ equality)* }
equality    =  { comparison ~ (eq_op ~ comparison)* }
comparison  =  { term ~ (cmp_op ~ term)* }
term        =  { factor ~ (add_op ~ factor)* }
factor      =  { unary ~ (mul_op ~ unary)* }
eq_op       = { "==" | "!=" }
cmp_op      = { "<=" | ">=" | "<" | ">" }
add_op      = { "+" | "-" }
mul_op      = { "*" | "/" }
unary       =  { ("!" | "-")* ~ primary }
primary     =  { func_call | number | string | identifier | "(" ~ expr ~ ")" }
func_call   =  { identifier ~ "(" ~ arg_list? ~ ")" }
arg_list    =  { expr ~ ("," ~ expr)* }